
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java'
    id 'com.google.protobuf' version('0.9.1')
    id 'signing'
    id 'org.jreleaser' version '1.17.0'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
    mavenCentral()
}

def grpcVersion = '1.51.0'
def protocVersion = "3.14.0"

dependencies {
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'com.google.protobuf:protobuf-java:3.21.12'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.amazonaws:aws-java-sdk:1.12.373'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'

    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'

    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    //Required as the protoc generates code that uses these annotations
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

task run_number {
    println 'Run number is: '+ System.getenv("run_number")
}

group = 'io.github.fintekkers'
version = System.getenv("run_number") ?: '0.0.1'

description = 'FinTekkers Ledger Models'
java.sourceCompatibility = JavaVersion.VERSION_17
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task sourcesJar(type: Jar) {
    duplicatesStrategy = 'include'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

java {
    withSourcesJar()
    withJavadocJar()
}

sourceSets {
    main {
        java {
            //The java specific hand-coded classes
            srcDirs 'src/main/java'

            //The GRPC auto-generated service defintions
            srcDirs 'generated-src/main/gprc'
            //The auto-generated model definitions
            srcDirs 'generated-src/main/java'
        }
        proto {
            //Above the java project folder level
            srcDirs '../ledger-models-protos'
        }
    }
    test {
        java {
            //Hand-coded java tests
            srcDirs 'src/test/java'
        }
    }
}

//So that the GitHub build doesn't fail for java doc issues
javadoc {
    failOnError = false
}

//Required for JUnit5
test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.fintekkers'
            artifactId = 'ledger-models'

            from components.java

            pom {
                name = 'fintekkers-ledger-models'
                description = 'Fintekkers Ledger Models'
                url = 'https://github.com/fintekkers/ledger-models'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'dado0583'
                        name = 'davidjdoherty@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/fintekkers/ledger-models.git'
                    developerConnection = 'scm:git:ssh://git@github.com/fintekkers/ledger-models.git'
                    url = 'https://github.com/fintekkers/ledger-models'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    release {
        github {
            enabled = false
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }


    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            
        }
    }
}

protobuf {
    protoc {
        //Specifying the protoc version & executable depending on the OS.
        if (osdetector.os == "osx") {
            //Need to specify the specific build version as GPRC
            //not compiled for native M1 chips
            artifact ="com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:${protocVersion}"
        }
    }

    generatedFilesBaseDir = 'generated-src'

    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}:osx-x86_64"
            } else {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}